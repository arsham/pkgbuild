# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Maintainer: Antoine Viallon <antoine@lesviallon.fr>

pkgname=ananicy-cpp-git
_pkgname=ananicy-cpp
pkgver=1.0.0.rc6.r7.g5a6f31f
pkgrel=1
pkgdesc="Ananicy Cpp is a full rewrite of Ananicy in C++, featuring lower CPU and RAM usage."
source=(
	"git+https://gitlab.com/ananicy-cpp/ananicy-cpp.git"
	"git+https://github.com/Nefelim4ag/Ananicy.git"
	"git+https://gitlab.com/ananicy-cpp/stl-polyfills/std-format.git"
  "fzf.rules"
  "go.rules"
  "lsp.rules"
  "nload.rules"
  "rofi.rules"
)
md5sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)
arch=(x86_64 i386 armv7h)
depends=(fmt spdlog nlohmann-json systemd)
makedepends=(cmake git gcc)
conflicts=(ananicy)
provides=(ananicy-cpp ananicy)
optdepends=("ananicy-rules-git: community rules")

pkgver() {
  cd "$_pkgname"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$_pkgname"

	git submodule init
	git config submodule."external/std-format".url "${srcdir}/std-format"
	git submodule update

	cmake -B build -S . \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_EXTERNAL_SPDLOG=ON \
		-DUSE_EXTERNAL_JSON=ON \
		-DUSE_EXTERNAL_FMTLIB=ON
}

build() {
	cd "$_pkgname"
	cmake --build build --target ananicy-cpp
}

package() {
	cd "$_pkgname"
	export DESTDIR="$pkgdir"
	cmake --install build --component Runtime

	install -m755 -d "$pkgdir/etc/"
  cp -r ../Ananicy/ananicy.d "$pkgdir/etc/ananicy.d"
  cp ../fzf.rules   "$pkgdir/etc/ananicy.d/00-default/"
  cp ../go.rules    "$pkgdir/etc/ananicy.d/00-default/"
  cp ../lsp.rules   "$pkgdir/etc/ananicy.d/00-default/"
  cp ../nload.rules "$pkgdir/etc/ananicy.d/00-default/"
  cp ../rofi.rules  "$pkgdir/etc/ananicy.d/00-default/"
}
